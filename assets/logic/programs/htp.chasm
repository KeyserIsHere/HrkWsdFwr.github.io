# HTP Packet Decoder
# Decodes HTP (Hashed Transfer Protocol) packets and forwards the decoded
# data to the next device.
#
# This is intended to be a standalone program that runs indefinitely. To
# use it, simply define an 'htp_in' and 'htp_out' for the appropriate ports.
#
# htp_in: Is the port that passes HTP packets to this program.
# It expects to receive only 1 byte at a time. The byte can be any HTP
# packet.
#
# The program can keep track of 5 unique HTP packet sequences at any given
# time. If there is not enough room for the new packet sequence, it will
# either purge an older one, or it will discard the incoming one.
#
# htp_out: Is the port this program will send the decoded data to.
# It will send the full block of data with the structure:
#
#   0 1 2 3 4 5 6 7
#  +-+-+-+-+-+-+-+-+
#  |     Size      |
#  +-+-+-+-+-+-+-+-+
#  |     Data ...
#  +-+-+-+-+-+-+-+-+
#
# Size: 1 byte
#    The size of the data
#
# Data: 1 to  bytes
#    The decoded data (of length Size)
#

.define htp_buffer_hash, 0
.define htp_buffer_timestamp, 1
.define htp_buffer_count, 2
.define htp_buffer_payload, 3
.define htp_buffer_payload_size, 4 #htp_buffer_payload_data_length_mask + 1
.define htp_buffer_payload_bitsize, 32 #htp_buffer_payload_size * 8
.define htp_buffer_payload_data_length, 3 #htp_buffer_payload
.define htp_buffer_payload_data_length_mask, 3
.define htp_buffer_payload_data, 4 #htp_buffer_payload + 1
.define htp_buffer_size, 7 #htp_buffer_payload_size + 3
.define htp_buffer_max, 60 #(256 - buffers) / htp_buffer_size * htp_buffer_size
.define htp_threshold, 8 # TODO: play around with value

.entrypoint
listen:
    recv htp_in, [packet]
    jnz hash
    add r3, htp_buffer_size
    cmp r3, htp_buffer_max
    jul find_complete_buffer
    xor r3, r3
find_complete_buffer:
    mov r2, [buffers + r3 + htp_buffer_hash]
    mov r1, r2
    and r1, 0x80
    jz listen
    and r2, htp_buffer_payload_data_length_mask
    jmp send_buffer
hash:
    add [counter], 1
    mov r0, [packet]
    umod r0, 0x7f
    add r0, 1
    xor r3, r3
    mov r2, 0xff
find_buffer:
    mov r1, r0
    xor r1, [buffers + r3 + htp_buffer_hash]
    jz set_buffer
    xor r1, r0
    jnz check_age
    mov r2, r3
check_age:
    mov r1, [counter]
    sub r1, [buffers + r3 + htp_buffer_timestamp]
    cmp r1, htp_threshold
    jul next_buffer
    mov r2, r3
next_buffer:
    add r3, htp_buffer_size
    cmp r3, htp_buffer_max
    jul find_buffer
    mov r3, r2
    not r2
    jz listen
init_buffer:
    mov [buffers + r3 + htp_buffer_count], 0
set_buffer:
    mov [buffers + r3 + htp_buffer_timestamp], [counter]
    mov r1, [packet]
    mov r2, [buffers + r3 + htp_buffer_count]
    mov r0, r2
    add r0, 1
    mov [buffers + r3 + htp_buffer_count], r0
    cmp r0, htp_buffer_payload_bitsize
    jul payload_within_bounds
    mov [buffers + r3 + htp_buffer_hash], 0
    jmp skip_set_hash
payload_within_bounds:
    mov r0, r1
    shr r0, 1
    mov [buffers + r3 + htp_buffer_hash], r0
skip_set_hash:
    and r1, 1
    udiv r2, 8
    add r2, r3
    shl [buffers + r2 + htp_buffer_payload], 1
    or [buffers + r2 + htp_buffer_payload], r1
    mov r2, [buffers + r3 + htp_buffer_payload_data_length]
    and r2, htp_buffer_payload_data_length_mask
    jz listen
    mov r1, r2
    mul r1, 8
    add r1, 8
    cmp r1, [buffers + r3 + htp_buffer_count]
    jnz listen
    mov r1, r2
    or r1, 0x80
    mov [buffers + r3 + htp_buffer_hash], r1
send_buffer:
    add r2, 1
    send htp_out, r2, [buffers + r3 + htp_buffer_payload]
    jz listen
    mov [buffers + r3 + htp_buffer_hash], 0
    jmp listen
packet: .byte 0
counter: .byte 0
buffers:
